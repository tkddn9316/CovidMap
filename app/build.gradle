plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
}
apply plugin: 'kotlin-kapt'

android {
    compileSdk 33

    defaultConfig {
        applicationId "app.map.covid"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_KEY", project.properties["API_KEY"] ?: "\"NOT_FOUND\""
        buildConfigField "String", "Client_ID", project.properties["X_NCP_APIGW_API_KEY_ID"] ?: "\"NOT_FOUND\""
        buildConfigField "String", "Client_Secret", project.properties["X_NCP_APIGW_API_KEY"] ?: "\"NOT_FOUND\""
        manifestPlaceholders = [Naver_Client_ID: X_NCP_APIGW_API_KEY_ID_MANIFEST]
        archivesBaseName = "CovidMap_${getBranchName()}-" + versionName + "-" + getDateTime()
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "CovidMap_D ${getBranchName()}"
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "CovidMap_D ${getBranchName()}"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        dataBinding = true
    }

//    flavorDimensions "default"
//    productFlavors {
//        // dev 설정
//        dev {
//            applicationIdSuffix ".${getBranchName()}"
//            dimension "default"
//            resValue "string", "app_name", "CovidMap_D ${getBranchName()}"
//        }
//
//        real {
//            resValue "string", "app_name", "CovidMap"
//        }
//    }
}

def getBranchName() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

static def getDateTime() {
    new Date().format('MMddhhmm')
}

dependencies {
    def roomVersion = "2.4.1"

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.android.support:multidex:1.0.3'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    // GSON
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    // Scalars
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    // Room DataBase
    kapt("androidx.room:room-compiler:$roomVersion")
    implementation("androidx.room:room-ktx:$roomVersion")
    implementation("androidx.room:room-rxjava3:$roomVersion")
    // 네이버 지도 SDK
    implementation 'com.naver.maps:map-sdk:3.16.2'
    implementation("com.google.android.gms:play-services-location:19.0.1")
    // rxjava
    implementation 'io.reactivex.rxjava3:rxjava:3.0.3'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:29.3.1')
    // Add the dependency for the Firebase SDK for Google Analytics
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth'
}